%{

    #include "hw3_output.hpp"
    #include <iostream>
    #include <stdlib.h>
    #include "types.hpp"
    #include "symbol_table.hpp"
    #include "bp.hpp"
    #include "code_generator.hpp"

    using namespace output;
    using std::vector;

    extern int yylex();
    extern int yylineno;
    
    int yyerror(const char * message);

    TablesStack* tables_stack = &(TablesStack::getTablesStack());
    CodeBuffer* code_buffer = &(CodeBuffer::instance());
    CodeGenerator* code_generator;
%}

%token VOID INT BYTE B BOOL OVERRIDE TRUE FALSE RETURN IF WHILE BREAK CONTINUE COMMA LBRACE RBRACE ID NUM STRING
%right ASSIGN
%left OR
%left AND
%left RELOP
%left PLUSMINUS
%left MULTDIV
%right NOT
%left LPAREN
%left RPAREN
%left SC
%right ELSE

%%

Program     :   Funcs { 
                    if (!tables_stack->mainExists()) {
                        errorMainMissing();
                        exit(1);
                    }
                    tables_stack->closeScope();
                    exit(0);
                };
Funcs       :       { }
            |   FuncDecl Funcs { }
            ;
FuncDecl    :   OverRide RetType ID LPAREN Formals {
                    OverrideNode* override = (OverrideNode*)$1;
                    RetTypeNode* returnType = (RetTypeNode*)$2;
                    IdNode* id = (IdNode*)$3;
                    FormalsNode* formals = (FormalsNode*)$5;

                    int res = tables_stack->declFunc(id->name, returnType->type, formals, override->isOverride, yylineno);
                    if(res == -1) {
                        exit(1);
                    }

                    $$ = new FuncDeclNode(returnType->type, id->name, formals->declarations);

                } RPAREN LBRACE Statements RBRACE {
                    tables_stack->curr_func_type = NULL_TYPE;
                    tables_stack->closeScope();   
                };
OverRide    :       { $$ = new OverrideNode(); }
            |   OVERRIDE { $$ = new OverrideNode(true); }
            ;
RetType     :   Type { 
                    TypeNode* type = (TypeNode*)$1;
                    $$ = new RetTypeNode(type->type);
                }
            |   VOID { 
                    $$ = new RetTypeNode(TYPE_VOID);
                }
            ;
Formals     :       { $$ = new FormalsNode(); }
            |   FormalsList { $$ = new FormalsNode(((FormalsListNode*)$1)->declarations); }
            ;
FormalsList :   FormalDecl { 
                    vector<FormalDeclNode*> vec;
                    vec.insert(vec.begin(), (FormalDeclNode*)$1);
                    $$ = new FormalsListNode(vec);
                }
            |   FormalDecl COMMA FormalsList { 
                    FormalDeclNode* formal_dec = (FormalDeclNode*)$1;
                    FormalsListNode* formal_list = (FormalsListNode*)$3;
                    formal_list->declarations.insert(formal_list->declarations.begin(), formal_dec);

                    $$ = new FormalsListNode(formal_list->declarations);
                }
            ;
FormalDecl  :   Type ID { 
                    IdNode* id = (IdNode*)$2;
                    TypeNode* type = (TypeNode*)$1;
                    if (tables_stack->isInCurrScope(id->name)) {
                        errorDef(yylineno, id->name);
                        exit(1);
                    }
                    $$ = new FormalDeclNode(type->type, id->name, code_generator->freshVar());
                };
Statements  :   Statement { }
            |   Statements Statement { }
            ;
Statement   :   LBRACE OpenScope Statements RBRACE CloseScope { }
            |   Type ID SC { 
                    TypeNode* type = (TypeNode*)$1;
                    IdNode* id = (IdNode*)$2;
                    if (tables_stack->symbDeclared(id->name)) {
                        errorDef(yylineno, id->name);
                        exit(1);
                    }
                    tables_stack->declVar(id->name, type->type);
                }
            |   Type ID ASSIGN Exp SC { 
                    TypeNode* type = (TypeNode*)$1;
                    IdNode* id = (IdNode*)$2;
                    ExpNode* exp = (ExpNode*)$4;
                    if (tables_stack->symbDeclared(id->name)) {
                        errorDef(yylineno, id->name);
                        exit(1);
                    }
                    else {
                        if (AssignLegality(type->type, exp->type)) {
                            tables_stack->declVar(id->name, type->type);
                        }
                        else {
                            errorMismatch(yylineno);
                            exit(1);
                        }
                    }
                }
            |   ID ASSIGN Exp SC { 
                    IdNode* id = (IdNode*)$1;
                    ExpNode* exp = (ExpNode*)$3;
                    if (!tables_stack->symbDeclared(id->name)) {
                        errorUndef(yylineno, id->name);
                        exit(1);
                    }
                    else {
                        TypesEnum type = tables_stack->getIDType(id->name);
                        if (!AssignLegality(type, exp->type)) {
                            errorMismatch(yylineno);
                            exit(1);
                        }
                    }
                }
            |   Call SC { }
            |   RETURN SC { 
                    if(tables_stack->curr_func_type != TYPE_VOID) {
                        errorMismatch(yylineno);
                        exit(1);
                    }
                }
            |   RETURN Exp SC { 
                    ExpNode* exp = (ExpNode*)$2;
                    if(!AssignLegality(tables_stack->curr_func_type, exp->type)) {
                        errorMismatch(yylineno);
                        exit(1);
                    }
                }
            |   IF LPAREN IsExpBool RPAREN OpenScope Statement CloseScope { }
            |   IF LPAREN IsExpBool RPAREN OpenScope Statement CloseScope ELSE OpenScope Statement CloseScope { }
            |   WHILE LPAREN IsExpBool RPAREN WhileScope Statement CloseScope { }
            |   BREAK SC { 
                    if(!tables_stack->isCurrScopeInWhile()) {
                        errorUnexpectedBreak(yylineno);
                        exit(1);
                    }
                }
            |   CONTINUE SC {
                    if(!tables_stack->isCurrScopeInWhile()) {
                        errorUnexpectedContinue(yylineno);
                        exit(1);
                    }
                }
            ;
Call        :   ID LPAREN ExpList RPAREN {
                    IdNode* id = (IdNode*)$1;
                    ExpListNode* expr_list = (ExpListNode*)$3;
                    if(!tables_stack->symbDeclared(id->name, true)) {
                        errorUndefFunc(yylineno, id->name);
                        exit(1);
                    }
                    int res = tables_stack->checkFuncParams(id->name, expr_list->exprs, yylineno);
                    if(res == -1) {
                        exit(1);
                    }
                    $$ = new CallNode(tables_stack->getFuncType(id->name, expr_list->exprs), id->name);
                }
            |   ID LPAREN RPAREN { 
                    IdNode* id = (IdNode*)$1;
                    if(!tables_stack->symbDeclared(id->name, true)){
                        errorUndefFunc(yylineno, id->name);
                        exit(1);
                    }
                    tables_stack->checkFuncParams(id->name, yylineno);
                    vector<ExpNode*> exprs;
                    $$ = new CallNode(tables_stack->getFuncType(id->name, exprs), id->name);
                }
            ;
ExpList     :   Exp { 
                    ExpNode* exp = (ExpNode*)$1;
                    vector<ExpNode*> exps;
                    exps.insert(exps.begin(), exp);
                    $$ = new ExpListNode(exps);
                }
            |   Exp COMMA ExpList { 
                    ExpNode* exp = (ExpNode*)$1;
                    ExpListNode* expr_list = (ExpListNode*)$3;
                    expr_list->exprs.insert(expr_list->exprs.begin(), exp);
                    $$ = new ExpListNode(expr_list->exprs);
                }
            ;
Type        :   INT { $$ = new TypeNode(TYPE_INT); }
            |   BYTE { $$ = new TypeNode(TYPE_BYTE); }
            |   BOOL { $$ = new TypeNode(TYPE_BOOL); }
            ;
Exp         :   LPAREN Exp RPAREN { $$ = new ExpNode(((ExpNode*)$2)->type); }
            |   Exp PLUSMINUS Exp { 
                    ExpNode* first = (ExpNode*)$1;
                    ExpNode* second = (ExpNode*)$3;
                    TypesEnum type = SumType(first->type, second->type);
                    if (type == NULL_TYPE) {
                        errorMismatch(yylineno);
                        exit(1);
                    }
                    $$ = new ExpNode(type);
                }
            |   Exp MULTDIV Exp { 
                    ExpNode* first = (ExpNode*)$1;
                    ExpNode* second = (ExpNode*)$3;
                    TypesEnum type = SumType(first->type, second->type);
                    if (type == NULL_TYPE) {
                        errorMismatch(yylineno);
                        exit(1);
                    }
                    $$ = new ExpNode(type);
                }
            |   ID { 
                    IdNode* id = (IdNode*)$1;
                    if(!tables_stack->symbDeclared(id->name)) {
                            errorUndef(yylineno, id->name);
                            exit(1);
                    }
                    $$ = new ExpNode(tables_stack->getIDType(id->name));
                }
            |   Call { $$ = new ExpNode(((CallNode*)$1)->type); }
            |   NUM { $$ = new ExpNode(TYPE_INT); }
            |   NUM B { 
                    NumNode* num = (NumNode*)$1;
                    if(std::stoi(num->num_val) > 255) {
                        errorByteTooLarge(yylineno, num->num_val);
                        exit(1);
                    }
                    $$ = new ExpNode(TYPE_BYTE);
                }
            |   STRING { $$ = new ExpNode(TYPE_STRING); }
            |   TRUE { $$ = new ExpNode(TYPE_BOOL); }
            |   FALSE { $$ = new ExpNode(TYPE_BOOL); }
            |   NOT Exp { 
                    ExpNode* exp = (ExpNode*)$2;
                    if (exp->type != TYPE_BOOL) {
                        errorMismatch(yylineno);
                        exit(1);
                    }
                    $$ = new ExpNode(TYPE_BOOL);
                }
            |   Exp AND Exp { 
                    ExpNode* first = (ExpNode*)$1;
                    ExpNode* second = (ExpNode*)$3;
                    if (first->type != TYPE_BOOL || second->type != TYPE_BOOL) {
                        errorMismatch(yylineno);
                        exit(1);
                    }
                    $$ = new ExpNode(TYPE_BOOL);
                }
            |   Exp OR Exp { 
                    ExpNode* first = (ExpNode*)$1;
                    ExpNode* second = (ExpNode*)$3;
                    if (first->type != TYPE_BOOL || second->type != TYPE_BOOL) {
                        errorMismatch(yylineno);
                        exit(1);
                    }
                    $$ = new ExpNode(TYPE_BOOL);
                }
            |   Exp RELOP Exp {
                    ExpNode* first = (ExpNode*)$1;
                    ExpNode* second = (ExpNode*)$3;
                    TypesEnum type = SumType(first->type, second->type);
                    if (type == NULL_TYPE) {
                        errorMismatch(yylineno);
                        exit(1);
                    }
                    $$ = new ExpNode(TYPE_BOOL);
                }
            |   LPAREN Type RPAREN Exp {
                    TypeNode* type = (TypeNode*)$2;
                    ExpNode* exp = (ExpNode*)$4;
                    if(!ConversionLegality(exp->type , type->type)) {
                        errorMismatch(yylineno);
                        exit(1);
                    }
                    $$ = new ExpNode(type->type);
                }
            ;

 IsExpBool  :   Exp { 
                    ExpNode* exp = (ExpNode*)$1;
                    if(exp->type != TYPE_BOOL) {
                        errorMismatch(yylineno);
                        exit(1);
                    }
                };
 OpenScope  :   { tables_stack->openScope(); };
 WhileScope :   { tables_stack->openScope(true); };
 CloseScope :   { tables_stack->closeScope(); };
%%

int main()
{
    tables_stack->Init();
    code_generator = new CodeGenerator(code_buffer);
	yyparse();
}

int yyerror(const char * message)
{
	errorSyn(yylineno);
	exit(1);
}